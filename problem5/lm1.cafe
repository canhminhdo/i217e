in ../pgm.cafe

-- ----------------------------------------------------------------------------
-- LEMMA1: \forall L1, L2 \in NatList: size(L1 @ L2) = size(L1) + size(L2) .
-- [require] PROBLEM1 - LEMMA4 [assoc+], PROBLEM1 - LEMMA5 [comm+]
-- ----------------------------------------------------------------------------

select NATLIST
--> PROOF: LEMMA1
--> base case
open NATLIST .
    op l2 : -> NatList .

    red size(nil @ l2) = size(nil) + size(l2) .
close

--> induction case
open NATLIST .
    op x : -> PNat .
    ops l1 l2 : -> NatList .

    vars I J K : PNat .

    -- induction hypothesis
    eq [IH] : size(l1 @ l2) = size(l1) + size(l2) .
    -- use PROBLEM1 - LEMMA4 [assoc+]
    eq [PROB1-LM4] : (I + J) + K = I + (J + K) .
    -- use PROBLEM1 - LEMMA5 [comm+]
    eq [PROB1-LM5] : size(l2) + I = I + size(l2) .
    -- induction case
    red size((x | l1) @ l2) = size(x | l1) + size(l2) .
close