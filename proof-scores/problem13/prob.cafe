in ../../pgm.cafe

-- ----------------------------------------------------------------------------
-- PROBLEM13: \forall I \in Nat, L \in NatList: count(L, I) = count(rev(L), I) .
-- [require] LEMMA1, PROBLEM1 - LEMMA5, PROBLEM1 - LEMMA6
-- ----------------------------------------------------------------------------

select NATLIST
--> PROOF: PROBLEM13
--> base case
open NATLIST .
    var I : PNat .

    red count(nil, I) = count(rev(nil), I) .
close

--> induction case
--> case 1
open NATLIST .
    ops x y : -> PNat .
    op l : -> NatList .
    
    var I : PNat .
    vars L1 L2 : NatList .

    -- induction hypothesis
    eq [IH] : count(l, y) = count(rev(l), y) .
    -- case splitting
    eq x = y .
    -- use LEMMA1
    eq count(L1 @ L2, I) = count(L1, I) + count(L2, I) .
    -- use PROBLEM1 - LEMMA5
    eq count(rev(l),y) + s(0) = s(0) + count(rev(l),y) .
    -- induction case
    red count((x | l), y) = count(rev(x | l), y) .
close

--> case 2
open NATLIST .
    ops x y : -> PNat .
    op l : -> NatList .
    
    var I : PNat .
    vars L1 L2 : NatList .

    -- induction hypothesis
    eq [IH] : count(l, y) = count(rev(l), y) .
    -- case splitting
    eq (x = y) = false .
    -- use LEMMA1
    eq count(L1 @ L2, I) = count(L1, I) + count(L2, I) .
    -- use PROBLEM1 - LEMMA6
    eq I + 0 = I .
    -- induction case
    red count((x | l), y) = count(rev(x | l), y) .
close