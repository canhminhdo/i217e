in ../../pgm.cafe

-- ----------------------------------------------------------------------------
-- LEMMA6: \forall I J K \in NAT: ((I < J) /\ (J < K)) implies (I < K) .
-- [require] 
-- ----------------------------------------------------------------------------

select PNAT
--> PROOF: LEMMA6
--> base case
--> bcase 1
open PNAT .
    ops j k : -> PNat .

    -- case splitting
    eq j = 0 .
    red ((0 < j) /\ (j < k)) implies (0 < k) .
close

--> bcase 2.1
open PNAT .
    ops j k y z : -> PNat .
    
    -- case splitting
    eq j = s(y) .
    eq k = 0 .
    red ((0 < j) /\ (j < k)) implies (0 < k) .
close

--> bcase 2.2
open PNAT .
    ops j k y z : -> PNat .

    -- case splitting
    eq j = s(y) .
    eq k = s(z) .
    red ((0 < j) /\ (j < k)) implies (0 < k) .
close

--> induction case
--> case 1
open PNAT .
    ops j k x : -> PNat .
    
    -- case spliting
    eq j = 0 .
    -- induction case
    red ((s(x) < j) /\ (j < k)) implies (s(x) < k) .
close

--> case 2.1
open PNAT .
    ops j k x y : -> PNat .
    
    -- case spliting
    eq j = s(y) .
    eq k = 0 .
    -- induction case
    red ((s(x) < j) /\ (j < k)) implies (s(x) < k) .
close

--> case 2.2
open PNAT .
    ops j k x y z : -> PNat .

    -- induction hypothesis
    eq [IH] : (((x < y) /\ (y < z)) implies (x < z)) = true .
    -- case spliting
    eq j = s(y) .
    eq k = s(z) . 
    -- induction case
    red ((s(x) < j) /\ (j < k)) implies (s(x) < k) .
close